# Messaging inbox query.
# %1 replaced with filters at runtime.

SELECT ?channel
  nie:subject(?channel)
  nie:generator(?channel)
  nie:identifier(?channel)
  nie:title(?channel)
  ?_lastDate 
  ( SELECT COUNT(?_total_messages_1)
    WHERE {
      ?_total_messages_1 nmo:communicationChannel ?channel .
      ?_total_messages_1 nmo:isDeleted false .
  })
  ( SELECT COUNT(?_total_unread_messages_1)
    WHERE {
      ?_total_unread_messages_1 nmo:communicationChannel ?channel .
      ?_total_unread_messages_1 nmo:isRead false .
      ?_total_unread_messages_1 nmo:isDeleted false .
  })
  ( SELECT COUNT(?_total_sent_messages_1)
    WHERE {
      ?_total_sent_messages_1 nmo:communicationChannel ?channel .
      ?_total_sent_messages_1 nmo:isSent true .
      ?_total_sent_messages_1 nmo:isDeleted false .
  })
  ?_lastMessage 
  (SELECT GROUP_CONCAT(fn:string-join((tracker:id(?contact), nco:nameGiven(?contact), nco:nameFamily(?contact), "\u001d", ?nicknames), "\u001e"), "\u001c")
   WHERE {
     SELECT ?contact
     (SELECT GROUP_CONCAT(fn:string-join((?addr, ?nickname), "\u001f"), "\u001e")
      WHERE {
        ?contact nco:hasAffiliation [ nco:hasIMAddress ?addr ] .
        ?addr nco:imNickname ?nickname .
      }) AS ?nicknames
     WHERE {
     {
       ?part nco:hasIMAddress ?address .
       ?contact nco:hasAffiliation [ nco:hasIMAddress ?address ] .
     } UNION {
       ?part nco:hasPhoneNumber [ maemo:localPhoneNumber ?number ] .
       ?contact nco:hasAffiliation [ nco:hasPhoneNumber [ maemo:localPhoneNumber ?number ] ] .
     }}
  }) AS ?_contacts
  rdf:nil
  fn:string-join((nmo:messageSubject(?_lastMessage),nie:plainTextContent(?_lastMessage)),"\u001e")
  nfo:fileName(nmo:fromVCard(?_lastMessage))
  rdfs:label(nmo:fromVCard(?_lastMessage))
  rdf:type(?_lastMessage) AS ?_type 
  nmo:deliveryStatus(?_lastMessage) AS ?_deliveryStatus 
  ?_lastModified 
WHERE 
{
  {
    SELECT ?channel ?_lastDate ?_lastModified ?part
      ( SELECT ?_message WHERE {
        ?_message nmo:communicationChannel ?channel .
        ?_message nmo:isDeleted false .
        ?_message nmo:sentDate ?messageSentDate .
      } ORDER BY DESC(?messageSentDate) DESC(tracker:id(?_message))
    LIMIT 1) AS ?_lastMessage 

    WHERE
    {
      GRAPH <commhistory:message-channels> {
        ?channel a nmo:CommunicationChannel .
      }
      ?channel nmo:lastMessageDate ?_lastDate .
      ?channel nie:contentLastModified ?_lastModified .
      ?channel nmo:hasParticipant ?part .
#      %1 
    }
  }
}
ORDER BY DESC(?_lastDate)
