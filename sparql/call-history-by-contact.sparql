# Calls grouped by contacts.

SELECT ?channel
  ?lastCall
  ?lastDate
  nmo:receivedDate(?lastCall)
  (SELECT GROUP_CONCAT(tracker:coalesce(nco:imID(?medium), nco:phoneNumber(?medium), ?medium), "\u001e")
   WHERE {
     ?lastCall nmo:from [ nco:hasContactMedium ?medium ] .
   })
  (SELECT GROUP_CONCAT(tracker:coalesce(nco:imID(?medium), nco:phoneNumber(?medium), ?medium), "\u001e")
   WHERE {
     ?lastCall nmo:to [ nco:hasContactMedium ?medium ] .
   })
  nmo:isSent(?lastCall)
  nmo:isAnswered(?lastCall)
  nmo:isEmergency(?lastCall)
  nmo:isRead(?lastCall)
  nie:contentLastModified(?lastCall)
  (SELECT GROUP_CONCAT(fn:string-join((tracker:id(?contact), nco:nameGiven(?contact), nco:nameFamily(?contact), "\u001d", fn:concat(nco:nickname(?contact), "\u001d"), ?nicknames), "\u001e"), "\u001c")
   WHERE {
     SELECT ?contact
     (SELECT GROUP_CONCAT(fn:string-join((?addr, ?nickname), "\u001f"), "\u001e")
      WHERE {
        ?contact nco:hasAffiliation [ nco:hasIMAddress ?addr ] .
        ?addr nco:imNickname ?nickname .
      }) AS ?nicknames
     WHERE {
     {
       ?part nco:hasIMAddress ?address .
       ?contact nco:hasAffiliation [ nco:hasIMAddress ?address ] .
     } UNION {
       ?part nco:hasPhoneNumber [ maemo:localPhoneNumber ?number ] .
       ?contact nco:hasAffiliation [ nco:hasPhoneNumber [ maemo:localPhoneNumber ?number ] ] .
     }}
  }) AS ?contacts
  rdf:nil
  ?missedCalls
WHERE 
{ 
  SELECT ?channel ?lastDate ?part
    ( SELECT ?lastCall
      WHERE {
        ?lastCall a nmo:Call .
        ?lastCall nmo:communicationChannel ?channel .
        ?lastCall nmo:sentDate ?lastCallDate .
      } ORDER BY DESC(?lastCallDate) DESC(tracker:id(?lastCall))
    ) AS ?lastCall
    ( SELECT COUNT(?missed)
      WHERE {
        ?missed a nmo:Call .
        ?missed nmo:communicationChannel ?channel .
        FILTER(nmo:sentDate(?missed) > nmo:lastSuccessfulMessageDate(?channel))
      }
    ) AS ?missedCalls

  WHERE
  {
    GRAPH <commhistory:call-channels> {
      ?channel a nmo:CommunicationChannel .
    }
    ?channel nmo:lastMessageDate ?lastDate .
    ?channel nmo:hasParticipant ?part .
  }
  ORDER BY DESC(?lastDate)
}
