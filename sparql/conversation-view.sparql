# Single conversation query.
# Replace <conversation:1> as needed.

SELECT
 ?message
 rdf:type(?message)
 nmo:sentDate(?message)
 ?endTime
 nmo:isSent(?message)
 nmo:isRead(?message)
 nmo:isEmergency(?message)
 nmo:deliveryStatus(?message)
 nie:contentSize(?message)
 (SELECT GROUP_CONCAT(tracker:coalesce(nco:imID(?medium), nco:phoneNumber(?medium), ?medium), "\u001e")
  WHERE {
    ?from nco:hasContactMedium ?medium .
  })
 (SELECT GROUP_CONCAT(tracker:coalesce(nco:imID(?medium), nco:phoneNumber(?medium), ?medium), "\u001e")
  WHERE {
    ?to nco:hasContactMedium ?medium .
  })
 nmo:messageSubject(?message)
 nie:plainTextContent(?message)
 nmo:communicationChannel(?message)
 nmo:messageId(?message)
 nfo:fileName(nmo:fromVCard(?message))
 rdfs:label(nmo:fromVCard(?message))
 nmo:encoding(?message)
 nie:characterSet(?message)
 nie:language(?message)
 nmo:isDeleted(?message)
 nmo:reportDelivery(?message)
 nmo:validityPeriod(?message)
 nie:generator(?message)
 (SELECT GROUP_CONCAT(fn:string-join((tracker:id(?contact), nco:nameGiven(?contact), nco:nameFamily(?contact), "\u001d", fn:concat(nco:nickname(?contact), "\u001d"), ?nicknames), "\u001e"), "\u001c")
  WHERE {
    SELECT ?contact
    (SELECT GROUP_CONCAT(fn:string-join((?addr, ?nickname), "\u001f"), "\u001e")
     WHERE {
       ?contact nco:hasAffiliation [ nco:hasIMAddress ?addr ] .
       ?addr nco:imNickname ?nickname .
     }) AS ?nicknames
    WHERE {
    {
      ?target nco:hasIMAddress ?address .
      ?contact nco:hasAffiliation [ nco:hasIMAddress ?address ] .
    } UNION {
      ?target nco:hasPhoneNumber [ maemo:localPhoneNumber ?number ] .
      ?contact nco:hasAffiliation [ nco:hasPhoneNumber [ maemo:localPhoneNumber ?number ] ] .
    }}
 }) AS ?contacts
 rdf:nil
 nmo:reportReadStatus(?message)
 nmo:sentWithReportRead(?message)
 nmo:mustAnswerReportRead(?message)
 nmo:mmsId(?message)
 (SELECT ?mmsTo {?message nmo:messageHeader [nmo:headerName "x-mms-to"; nmo:headerValue ?mmsTo]})
WHERE
{
  SELECT ?message ?startTime ?endTime ?from ?to
    IF (nmo:isSent(?message) = true, ?to, ?from) AS ?target
  WHERE {
    ?message nmo:from ?from ;
      nmo:to ?to ;
      nmo:receivedDate ?endTime ;
      nmo:sentDate ?startTime .
    ?message nmo:isDraft "false";
      nmo:isDeleted "false" .
    ?message nmo:communicationChannel <conversation:1> .
    ?message a nmo:Message .
  }
} ORDER BY DESC(?endTime) DESC(tracker:id(?message))
