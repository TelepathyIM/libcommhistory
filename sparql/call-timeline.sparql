# Calls ordered by time. CallModel groups similar events when processing
# query results.

SELECT
 ?message
 rdf:type(?message)
 nmo:sentDate(?message)
 ?endTime
 nmo:isSent(?message)
 nmo:isAnswered(?message)
 nmo:isRead(?message)
 nmo:isEmergency(?message)
 nie:contentSize(?message)
 (SELECT GROUP_CONCAT(tracker:coalesce(nco:imID(?medium), nco:phoneNumber(?medium), ?medium), "\u001e")
  WHERE {
    ?from nco:hasContactMedium ?medium .
  })
 (SELECT GROUP_CONCAT(tracker:coalesce(nco:imID(?medium), nco:phoneNumber(?medium), ?medium), "\u001e")
  WHERE {
    ?to nco:hasContactMedium ?medium .
  })
 nmo:communicationChannel(?message)
 nie:generator(?message)
  (SELECT GROUP_CONCAT(
    fn:concat(tracker:id(?contact), "\u001e",
              tracker:coalesce(nco:nameGiven(?contact), ""), "\u001e",
              tracker:coalesce(nco:nameFamily(?contact), ""),
              "\u001d",
              tracker:coalesce(nco:nickname(?contact), ""),
              "\u001d",
              tracker:coalesce(?nicknames, "")),
    "\u001c")
  WHERE {
    SELECT ?contact
    (SELECT GROUP_CONCAT(fn:string-join((nco:imID(?addr), ?nickname), "\u001f"), "\u001e")
     WHERE {
       ?contact nco:hasAffiliation [ nco:hasIMAddress ?addr ] .
       ?addr nco:imNickname ?nickname .
     }) AS ?nicknames
    WHERE {
    {
      ?target nco:hasIMAddress ?address .
      ?contact nco:hasAffiliation [ nco:hasIMAddress ?address ] .
    } UNION {
      ?target nco:hasPhoneNumber [ maemo:localPhoneNumber ?number ] .
      ?contact nco:hasAffiliation [ nco:hasPhoneNumber [ maemo:localPhoneNumber ?number ] ] .
    }}
 }) AS ?contacts
WHERE
{
  SELECT ?message ?startTime ?endTime ?from ?to
    IF (nmo:isSent(?message) = true, ?to, ?from) AS ?target
  WHERE {
    ?message nmo:from ?from ;
      nmo:to ?to ;
      nmo:receivedDate ?endTime ;
      nmo:sentDate ?startTime .
    ?message a nmo:Call .
  }
} ORDER BY DESC(?endTime) DESC(tracker:id(?message))
